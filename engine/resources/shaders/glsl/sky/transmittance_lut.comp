#version 460
// per MM, as per: https://www.shadertoy.com/view/slSXRW
const float PLANET_RADIUS = 6.360;
const float ATMOSPHERE_RADIUS = 6.460;
const vec3 RAYLEIGH_SCATTERING = vec3(5.802, 13.558, 33.1);
const float RAYLEIGH_ABSORPTION = 0.0;
const float MIE_SCATTERING = 21.996;
const float MIE_ABSORPTION = 4.4;
const vec3 OZONE_ABSORPTION = vec3(0.650, 1.881, .085);



layout(local_size_x = 4, local_size_y = 4, local_size_z = 1) in;

layout(set = 0, binding = 0) uniform writeonly image2D lut;

const float TRANSMITTANCE_STEPS = 80.0;

float intersect_sphere(vec3 o, vec3 d, float r) {
    float b = dot(o, d);
    float c = dot(o, o) - r*r;
    if (c > 0.0f && b > 0.0) return -1.0;
    float discr = b*b - c;
    if (discr < 0.0) return -1.0;
    if (discr > b*b) return (-b + sqrt(discr));
    return -b - sqrt(discr);
}

void get_scattering_values(
    vec3 p,
    out vec3 rayleigh_scattering,
    out float mie_scattering,
    out vec3 extinction
) {
    float altitude = (length(p) - PLANET_RADIUS) * 1000.0; // km

    float rayleigh_density = exp(-altitude/8.0);
    float mie_density = exp(-altitude/1.2);

    rayleigh_scattering = RAYLEIGH_SCATTERING * rayleigh_density;
    float rayleigh_absorption = RAYLEIGH_ABSORPTION * rayleigh_density;

    mie_scattering = MIE_SCATTERING * mie_density;
    float mie_absorption = MIE_ABSORPTION * mie_density;

    vec3 ozone_absorption = OZONE_ABSORPTION * max(0.0, 1.0 - abs(altitude - 25.0) / 15.0);

    extinction = rayleigh_scattering + rayleigh_absorption + mie_scattering + mie_absorption + ozone_absorption;
}
vec3 sun_transmittance(vec3 p, vec3 sun_dir) {
    if (intersect_sphere(p, sun_dir, PLANET_RADIUS) > 0.0) {
        return vec3(0.0);
    }

    float t_max = intersect_sphere(p, sun_dir, ATMOSPHERE_RADIUS);
    float t = 0.0;

    vec3 transmittance = vec3(1.0);
    for (float i = 0.0; i < TRANSMITTANCE_STEPS; i += 1.0) {
        float t_n = ((i + 0.3) / TRANSMITTANCE_STEPS) * t_max;
        float dt = t_n - t;
        t = t_n;

        vec3 sample_p = p + t * sun_dir;

        vec3 rayleigh_scattering, extinction;
        float mie_scattering;
        get_scattering_values(sample_p, rayleigh_scattering, mie_scattering, extinction);

        transmittance *= exp(-dt * extinction);
    }
    return transmittance;
}

void main() {
    ivec3 coord = ivec3(gl_GlobalInvocationID.xyz);

    vec2 uv = (vec2(coord) + 0.5) / vec2(imageSize(lut).xy);

    float sun_cos_theta = 2.0 * uv.x - 1.0;
    float height = mix(PLANET_RADIUS, ATMOSPHERE_RADIUS, uv.y);

    vec3 p = vec3(0.0, height, 0.0);
    float sun_sin_theta = sqrt(max(0.0, 1.0 - sun_cos_theta * sun_cos_theta));
    vec3 sun_dir = vec3(0.0, sun_cos_theta, -sun_sin_theta);

    imageStore(lut, coord.xy, vec4(sun_transmittance(p, sun_dir), 1.0));
}